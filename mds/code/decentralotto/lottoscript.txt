/*
  LOTTO SCRIPT

  Funky little script.. with comments. Works as is in Script IDE. 
   
  0 - Round Number
   
  PLAYER 1 puts their game details
  1 - PLAYER 1 PUBKEY
  2 - PLAYER 1 HASH OF SECRET
  3 - PLAYER 1 REQUESTED ODDS
  4 - PLAYER 2 PUBKEY
  5 - PLAYER 2 PAYOUT ADDRESS

  PLAYER 2 adds his details
  6 - RANDOM HASH VALUE
  
  Then player 1 reveals..
  7 - PLAYER 1 SECRET

*/

/* What state are we at */
LET round = STATE(0) 
LET prevround = PREVSTATE(0) 

/* Make sure we are 1 round ahead of before */
ASSERT round EQ INC(prevround) 

/* Define some useful vars */
LET playerpubkey = PREVSTATE(1)
LET secret = PREVSTATE(2)
LET odds = PREVSTATE(3)
LET chance = 1 / odds
LET lottopubkey  = PREVSTATE(4)
LET lottoaddress = PREVSTATE(5)

/* PLAYER 2 Joins OR Player 1 Cancels */
IF round EQ 1 THEN 

    /* Player 1 can still cancel at this stage */
    IF SIGNEDBY(playerpubkey) THEN RETURN TRUE ENDIF 

    /* Make sure all the details are kept */
    ASSERT SAMESTATE (1 5)

    /* Check the rand number specified correctly */
    ASSERT ( (HEX(STATE(6)) EQ STATE(6)) AND (LEN(STATE(6)) LTE 32) )
    
    /* Make sure correct output exists and stores the state */
    LET requiredamount = @AMOUNT * odds
    RETURN VERIFYOUT ( @INPUT @ADDRESS requiredamount @TOKENID TRUE )

/* PLAYER 1 REVEALS HIS HAND */
ELSEIF round EQ 2 THEN 

    /* Player has 64 blocks to claim - or Lottoe can claim it all */
    IF @COINAGE GT 64 AND SIGNEDBY(lottopubkey) THEN RETURN TRUE ENDIF 

    /* Check the provided preimage is correct */
    LET preimage  = STATE(7)
    LET checkhash = SHA3(preimage)
    ASSERT checkhash EQ secret

    /* NOW hash the 2 value */
    LET decider = SHA3(CONCAT(preimage PREVSTATE(6)))

    /* Calculate the number value of the first 6 bytes */
    LET hexsubset = SUBSET(0 6 decider)
    LET numvalue = NUMBER(hexsubset)
    LET maxvalue = NUMBER(0xFFFFFFFFFFFF)
    LET target = FLOOR(maxvalue * chance)

    /* Check if a winner */
    LET iswin = numvalue LTE target
    IF iswin THEN 

        /* Player gets the money */ 
        RETURN SIGNEDBY(playerpubkey)
    
    ELSE
         
        /* Send to Lotto address */
        RETURN VERIFYOUT ( @INPUT lottoaddress @AMOUNT @TOKENID TRUE )

    ENDIF

ENDIF

------------------------

LET round=STATE(0) LET prevround=PREVSTATE(0) ASSERT round EQ INC(prevround) LET playerpubkey=PREVSTATE(1) LET secret=PREVSTATE(2) LET odds=PREVSTATE(3) LET chance=1/odds LET lottopubkey=PREVSTATE(4) LET lottoaddress=PREVSTATE(5) IF round EQ 1 THEN IF SIGNEDBY(playerpubkey) THEN RETURN TRUE ENDIF ASSERT SAMESTATE(1 5) ASSERT ((HEX(STATE(6)) EQ STATE(6)) AND (LEN(STATE(6)) LTE 32)) LET requiredamount=@AMOUNT*odds RETURN VERIFYOUT(@INPUT @ADDRESS requiredamount @TOKENID TRUE) ELSEIF round EQ 2 THEN IF @COINAGE GT 64 AND SIGNEDBY(lottopubkey) THEN RETURN TRUE ENDIF LET preimage=STATE(7) LET checkhash=SHA3(preimage) ASSERT checkhash EQ secret LET decider=SHA3(CONCAT(preimage PREVSTATE(6))) LET hexsubset=SUBSET(0 6 decider) LET numvalue=NUMBER(hexsubset) LET maxvalue=NUMBER(0xFFFFFFFFFFFF) LET target=FLOOR(maxvalue*chance) LET iswin=numvalue LTE target IF iswin THEN RETURN SIGNEDBY(playerpubkey) ELSE RETURN VERIFYOUT(@INPUT lottoaddress @AMOUNT @TOKENID TRUE) ENDIF ENDIF


